def TEXT: 1954047348

// Converts an UTF-8 string into a list of bytes
def to_chars: {str}
  {Cons}
  dup Cons = Cons
  let Cons-non-0 = {c}
    cpy c = c
    if |c > 0|
    then: (Cons c)
    else: {x} x
  dup f =
    let func = {c cs}
      cpy c = c
      let Cons-c0 = (Cons-non-0 ||c >>  0| & 0xFF|)
      let Cons-c1 = (Cons-non-0 ||c >>  8| & 0xFF|)
      let Cons-c2 = (Cons-non-0 ||c >> 16| & 0xFF|)
      let Cons-c3 = (Cons-non-0 ||c >> 24| & 0xFF|)
      (Cons-c0 (Cons-c1 (Cons-c2 (Cons-c3 cs))))
    (snd str #func)
  # {nil} (f nil)

// Converts a list of bytes back to an UTF-8 string
def from_chars: {chars}
  let nums =
    {Cons}
    dup Cons = Cons
    dup func =
      let loop = {c cont char indx}
        cpy indx = indx
        cpy char = ||char >> 8| | |c << 24||
        cpy done = ||indx % 4| == 3|
        let Cons-char =
          if done
          then: (Cons char)
          else: {x}x
        let next-char =
          if done
          then: 0
          else: char
        (Cons-char (cont next-char |indx + 1|))
      (chars #loop)
    # {nil} (func {char indx}(Cons char nil) 0 0)
  [TEXT, nums]

// Converts a String to a utf-8 codepoint list (little endian)
def to_codepoints: {str}
  {Cons}
  dup Cons = Cons
  let loop_aux = {x h}
    cpy x = x
    (if |x > 0| then:
      {h i}
      cpy i = i
      (if |i == 0| then:
        if       |x < 0x80|                then: {h Cons x} (Cons x (h 0))
        else: if ||x > 0xc1| * |x < 0xe0|| then: {h Cons x} (h 1 {y} |x + |y << 8||)
        else: if ||x > 0xdf| * |x < 0xf0|| then: {h Cons x} (h 2 {y} |x + |y << 8||)
        else: if ||x > 0xef| * |x < 0xf5|| then: {h Cons x} (h 3 {y} |x + |y << 8||)
        else: {h Cons x} (Cons 0xBDBFEF (h 0)) // Replacement Character: "�"
      else:
        if |i == 1| then: {h Cons x} {k} (Cons (k x) (h 0))
        else:             {h Cons x} {k} (h |i - 1| {y} (k |x + |y << 8||))
      h Cons x)
    else: {h} h
    h)
  let loop = {c h i}
    cpy c = c
    (loop_aux ||c >>  0| & 0xFF| (loop_aux ||c >>  8| & 0xFF| (loop_aux ||c >> 16| & 0xFF| (loop_aux ||c >> 24| & 0xFF| h))) i)
  dup rec = (snd str #loop)
  #{Nil} (rec {i} Nil 0)

// Converts a utf-8 codepoint list to a String
def from_codepoints: {codepoints} 
  (from_chars (to_chars [TEXT, codepoints]))

// Convert from Unicode Codepoint to UTF-8 Numeric Character Reference
def codepoint_to_ncr: {codepoint}
  let conv = {x}
    cpy x = x
    (if       |x < 0x80|        then:  x
     else: if |x < 0xBFE0|      then:  |||x & 0x1F| << 6| | ||x >> 8| & 0x3F||
     else: if |x < 0xBFBFF0|    then:  ||||x & 0x0F| << 12| | ||x & 0x3F00| >> 2|| | ||x >> 16| & 0x3F||
     else: if |x < 0xBFBF8FF5|  then:  |||||x & 0x07| << 18| | ||x & 0x3F00| << 4|| | ||x & 0x3F0000| >> 10|| | ||x & 0x3F000000| >> 24||
     else: 0xBDBFEF) // Replacement Character: "�"
  (conv codepoint)

// Convert from UTF-8 Numeric Character Reference to Unicode Codepoint
def ncr_to_codepoint: {ncr}
  let conv = {x}
    cpy x = x
    (if       |x < 0x80|     then:  x
     else: if |x < 0x800|    then:  ||||x >> 6| & 0x1F| | 0xC0| + |||x & 0x3F| | 0x80| << 8||
     else: if |x < 0x10000|  then:  |||||x & 0x3F| | 0x80| << 16| + ||||x >> 6| & 0x3F| | 0x80| << 8|| + |||x >> 12| & 0x0F| | 0xE0||
     else: if |x < 0x110000| then:  ||||||x & 0x3F| | 0x80| << 24| + ||||x >> 6| & 0x3F| | 0x80| << 16|| + ||||x >> 12| & 0x3F| | 0x80| << 8|| + |||x >> 18| & 0x07| | 0xF0||
     else: 0xFFFD) // Replacement Character: "�"
  (conv ncr)

def string_concat: {a b}
  (from_chars (concat (to_chars a) (to_chars b)))
